#CQ-E1 What kind of event was [Event]
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
SELECT ?eventName ?type
       (GROUP_CONCAT(DISTINCT ?description; separator=" ") AS ?descriptionOfEvent)
WHERE {
    ?event rdf:type ?type .
    ?event schema:name ?eventName .
    ?event schema:description ?description .
    FILTER(?event = ex:LiveAid1985)
}
GROUP BY ?eventName ?type

#CQ‑E2 Why was [Event] organized?
PREFIX schema: <http://schema.org/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?eventName (GROUP_CONCAT(DISTINCT ?purpose; separator=", ") AS ?purposeOfTheEvent)
WHERE {
    ?event schema:about ?purpose .
    ?event schema:name ?eventName .
    FILTER(?event = ex:LiveAid1985)
}
GROUP BY ?eventName


#CQ‑E3 Which [Artist] performed at [Event] held at [Venue]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
SELECT DISTINCT ?eventName ?venueName
(GROUP_CONCAT(DISTINCT ?artistName; separator=", ") AS ?allPerformingArtists)
WHERE {
    ?event a schema:MusicEvent ;
           schema:location ?venue ;
           schema:performer ?artist .
    ?event schema:name ?eventName .
    ?venue schema:name ?venueName .
    ?artist schema:name ?artistName .
    FILTER(?event = ex:LiveAid1985)
    FILTER(?venue = ex:WembleyStadium)
}
GROUP BY ?eventName ?venueName


#CQ‑E4 Which [Artist] performed during [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
SELECT DISTINCT ?eventName (GROUP_CONCAT(DISTINCT ?artistName; separator=", ") AS ?allPerformingArtists)
WHERE {
    ?event a schema:MusicEvent ;
           schema:performer ?artist .
    ?event schema:name ?eventName .
    ?artist schema:name ?artistName .
    FILTER(?event = ex:LiveAid1985)
}
GROUP BY ?eventName

#CQ‑E5 Which [City] or [Country] hosted [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
SELECT DISTINCT ?eventName ?locationName ?cityName ?countryName
WHERE {
    ?event a schema:MusicEvent ;
           schema:location ?location .
    ?event schema:name ?eventName .
    OPTIONAL { ?location schema:location ?city .
        	   ?location schema:name ?locationName .
    			}
    OPTIONAL { ?city schema:containedInPlace ?country .
        		?city schema:name ?cityName .
        		?country schema:name ?countryName .
    			}
    FILTER(?event = ex:LiveAid1985)
}

#CQ‑E6 Which [Venue] hosted [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
SELECT DISTINCT ?eventName (GROUP_CONCAT(DISTINCT ?venueName; separator=", ") AS ?allVenues)
WHERE {
    ?event a schema:MusicEvent ;
           schema:location ?venue .
    ?venue a schema:Place ;
           schema:name ?venueName .
    ?event schema:name ?eventName .
    FILTER(?event = ex:LiveAid1985)
}
GROUP BY ?eventName


#CQ‑E7 What role did television and media coverage play in [Event]?
PREFIX schema: <http://schema.org/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?eventName ?description ?about
WHERE {
  ?broadcastEvent a schema:BroadcastEvent ;
                  schema:broadcastOfEvent ex:LiveAid1985 ;
                  schema:name ?eventName ;
                  schema:description ?description ;
                  schema:about ?about .
}

#CQ-E8 Who organized [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
SELECT DISTINCT ?eventName (GROUP_CONCAT(DISTINCT ?organizerName; separator=", ") AS ?allOrganizers)
WHERE {
    ?event schema:organizer ?organizer .
    ?event schema:name ?eventName .
    OPTIONAL { ?organizer schema:name ?organizerName }
    FILTER(?event = ex:LiveAid1985)
}
GROUP BY ?eventName


#CQ-E9 Which [Performance] at [Event] is considered iconic?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?artistName ?description ?isIconic (GROUP_CONCAT(DISTINCT ?songName; separator=", ") AS ?songsPerformed)
WHERE {
    ?performance a mm:LivePerformance ;
                 schema:isPartOf ?event ;
                 mm:isCreatedBy ?artist ;
                 mm:creates ?song ;
    			 ex:isIconic ?isIconic ;
                 schema:description ?description .
    ?artist schema:name ?artistName .
    OPTIONAL {?song schema:itemListElement ?songEntry .
    			?songEntry schema:name ?songName .}
    FILTER(?event = ex:LiveAid1985)
    FILTER(?isIconic = true)
}
GROUP BY ?artistName ?description ?isIconic


#CQ-E10 Which [Song] did [Artist] perform at [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?artistname (GROUP_CONCAT(DISTINCT ?performedSong; separator=", ") AS ?songsPerformed)
WHERE {
    ?performance a mm:LivePerformance ;
                 schema:isPartOf ?event ;
                 mm:isCreatedBy ?artist ;
                 mm:creates ?setlist .
    FILTER(?event = ex:LiveAid1985)
    FILTER(?artist = ex:Queen)
    ?artist schema:name ?artistname .
    ?setlist schema:itemListElement ?items .
    ?items schema:name ?performedSong .
}
GROUP BY ?artistname


#CQ-E11 When did [Artist] debut?,[Artist] debuted in [Year] with a performance or release in [City/Country].
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?artistName ?debutYear ?debutName ?debutLabelName ?releasedDate
WHERE {
    ?artist a ?type ;
            schema:name ?artistName ;
            ex:debutYear ?debutYear ;
            ex:hasDebutRelease ?debutRelease .
    OPTIONAL { ?debutRelease schema:datePublished ?releasedDate }
    OPTIONAL { ?debutRelease schema:name ?debutName }
    OPTIONAL { ?debutRelease mm:hasPublisher ?debutLabel }
    OPTIONAL { ?debutLabel schema:name ?debutLabelName }
    FILTER(?artist = ex:Madonna)
}


#CQ-E12 When was [Artist] formed?,[Artist] was formed in [Year] in [City/Country].
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
SELECT DISTINCT
    ?artistName
    ?formationYear
    (SAMPLE(?cityName) AS ?formationCityName)
    (SAMPLE(?countryName) AS ?formationCountryName)
WHERE {
    ?artist schema:name ?artistName .
    OPTIONAL {
        ?artist schema:foundingDate ?formationYear .
    }
    OPTIONAL {
        ?artist schema:foundingLocation ?city .
        ?city a schema:City ;
              schema:name ?cityName .
        OPTIONAL {
            ?city schema:containedInPlace ?country .
            ?country schema:name ?countryName .
        }
    }
    FILTER(?artist = ex:Queen)
}
GROUP BY ?artist ?artistName ?formationYear



#CQ-E13 Who were the members of [Artist]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?artistName (GROUP_CONCAT(DISTINCT ?memberName; separator=", ") AS ?allMembers)
WHERE {
    ?artist schema:name ?artistName .
    OPTIONAL {
        ?artist schema:member ?member .
        ?member schema:name ?memberName .
    }
    FILTER(?artist = ex:Queen)
}
GROUP BY ?artistName


#CQ-E14 Who were the solo artists that performed at [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?eventName (GROUP_CONCAT(DISTINCT ?artistName; separator=", ") AS ?allSingleArtists)
WHERE {
    ?event schema:performer ?artist .
    ?event schema:name ?eventName .
    ?artist a schema:Person ;
            schema:name ?artistName .
    FILTER(?event = ex:LiveAid1985)
}
GROUP BY ?eventName



#CQ-E15 What [Genre] are associated with [Artist]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?artistName (GROUP_CONCAT(DISTINCT ?genreLabel; separator=", ") AS ?allGenres)
WHERE {
    ?artist schema:name ?artistName .
    { ?artist schema:genre ?genre } UNION { ?artist mm:hasGenre ?genre } .
    OPTIONAL { ?genre schema:name ?genreLabel . }
    OPTIONAL { ?genre rdfs:label ?genreLabel . }
    BIND(STRAFTER(STR(?genre), "#") AS ?localName)
    FILTER(?artist = ex:Queen)
}
GROUP BY ?artistName

#CQ-E16 What enabled the global broadcast of [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?eventName ?enabler ?enablerLabel
WHERE {
    ?event ex:usedBroadcastInfrastructure|ex:wasBroadcastVia ?enabler .
    ?event schema:name ?eventName .
    OPTIONAL { ?enabler rdfs:label ?lbl1 }
    OPTIONAL { ?enabler schema:name ?lbl2 }
    BIND(STRAFTER(STR(?enabler), "#") AS ?fallback)
    BIND(COALESCE(?lbl1, ?lbl2, ?fallback) AS ?enablerLabel)
    FILTER(?event = ex:LiveAid1985)
}
ORDER BY ?enablerLabel


#CQ-E17 What were the challenges in organizing [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?eventName ?challenge
WHERE {
    VALUES ?event { ex:LiveAid1985 }
    ?event schema:name ?eventName .
	?event ex:hasChallenge ?challenge .
}


#CQ-E18,,How did [Event] influence future benefit concerts?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?eventName ?inspiredEventName ?inspiredEventType ?inspiredEventDate ?inspiredCity ?inspiredCountryName
WHERE {
    ?event schema:name ?eventName ;
           schema:description ?description ;
           ex:inspiredEvent ?inspiredEvent .
    OPTIONAL { ?inspiredEvent schema:name ?inspiredEventName . }
    OPTIONAL { ?inspiredEvent a ?inspiredEventType . }
    OPTIONAL { ?inspiredEvent schema:startDate ?inspiredEventDate . }
    OPTIONAL { ?inspiredEvent schema:location ?inspiredLocation .
    			?inspiredLocation schema:name ?inspiredCity .
    			?inspiredLocation schema:containedInPlace ?inspiredCountry .
        		?inspiredCountry schema:name ?inspiredCountryName .
    			}
    FILTER(?event = ex:LiveAid1985)
}


#CQ-E19 What were the reactions of the audience or media to [Artist]’s [Performance] at [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX schema: <http://schema.org/>
SELECT DISTINCT ?artistName ?eventName ?reviewerName ?reviewText
WHERE {
    ?performance mm:isCreatedBy ?artist ;
                 schema:isPartOf|schema:isPartOf ?event .
    ?artist schema:name ?artistName .
    ?event schema:name ?eventName .
    OPTIONAL {
        ?performance schema:review ?review .
        ?review schema:reviewBody ?reviewText .
        ?review schema:author ?reviewer .
        ?reviewer schema:name ?reviewerName .
    }
    OPTIONAL {
        ?performance schema:comment ?audienceComment
    }
    FILTER(?artist = ex:Queen)
    FILTER(?event = ex:LiveAid1985)
}


#CQ-E20,,How much money was raised by [Event]?,
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT ?eventName ?currency ?raisedAmountValue
WHERE {
    VALUES ?event { ex:LiveAid1985 }
    ?event ex:raisedAmount ?raisedAmount .
    ?amount a schema:MonetaryAmount .
    ?event schema:name ?eventName .
    OPTIONAL { ?amount schema:currency ?currency }
    OPTIONAL { ?amount schema:value ?raisedAmountValue }
}

#CQ-E21 Which [Organization] archived the broadcast of [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?eventName (GROUP_CONCAT(DISTINCT ?broadcastingCompanyName; separator=", ") AS ?broadcastingCompanies)
WHERE {
    BIND (ex:LiveAid1985 as ?event )
    ?event ex:hadBroadcast ?broadcast .
    ?event schema:name ?eventName .
    OPTIONAL { ?broadcast schema:provider ?broadcastingCompany . }
    OPTIONAL { ?broadcastingCompany schema:name ?broadcastingCompanyName }
    OPTIONAL { ?broadcastingCompany rdfs:label ?broadcastingCompanyName }
}
GROUP BY ?eventName


#CQ-E22 Which[Organization] failed to archive the broadcast of [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT ?eventName ?subBroadcastDescription ?broadcastCompanyName ?preserved
WHERE {
    BIND (ex:LiveAid1985 as ?mainEvent)
    ?mainEvent schema:name ?eventName .
    ?mainEvent ex:hadBroadcast ?mainBroadcast .
    ?mainBroadcast schema:subEvent ?subBroadcast .
    ?subBroadcast schema:description ?subBroadcastDescription .
    ?subBroadcast schema:provider ?broadcastCompany .
    ?subBroadcast ex:wasBroadcastPreserved ?preserved .
    OPTIONAL { ?broadcastCompany schema:name ?broadcastCompanyName }
    OPTIONAL { ?broadcastCompany rdfs:label ?broadcastCompanyName }
    FILTER(?preserved = false)
}
ORDER BY DESC(?preserved)


#CQ-E23 What is the preservation status of [Event] broadcasts at [Venue]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?eventName ?subBroadcastDescription ?broadcastCompanyName ?preserved
WHERE {
    VALUES ?mainEvent { ex:LiveAid1985 }
    ?mainEvent ex:hadBroadcast ?mainBroadcast .
    ?mainEvent schema:name ?eventName .
    ?mainBroadcast schema:subEvent ?subBroadcast .
    ?subBroadcast schema:description ?subBroadcastDescription .
    ?subBroadcast schema:provider ?broadcastCompany .
    ?subBroadcast ex:wasBroadcastPreserved ?preserved .
    OPTIONAL { ?broadcastCompany schema:name ?broadcastCompanyName }
    OPTIONAL { ?broadcastCompany rdfs:label ?broadcastCompanyName }
}
ORDER BY DESC(?preserved)

#CQ-E24 Why is [Artist]’s performance at [Event] considered iconic?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT ?performance ?reason
WHERE {
    ?performance a mm:LivePerformance ;
                 schema:isPartOf ex:LiveAid1985 ;
                 mm:isCreatedBy ex:Queen ;
                 schema:review ?review .
    ?review schema:reviewBody ?reason .
}

#CQ-E27 What creative milestones defined [Artist]’s career before and after [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?artistName ?title ?year ?period ?eventName ?sentence
WHERE {
    BIND(ex:Queen AS ?artist)
    BIND(ex:LiveAid1985 AS ?event)
    ?event schema:startDate ?eventDate ;
           schema:name ?eventName .
    ?artist schema:name ?artistName ;
            mm:creates ?work .
    ?work schema:name ?title ;
          schema:datePublished ?year .
    BIND(
        IF(xsd:date(?year) < xsd:date(?eventDate),
           "Before Event",
           "After Event"
        ) AS ?period
    )
    BIND(
        IF(xsd:date(?year) < xsd:date(?eventDate),
           CONCAT("Before ", ?eventName, ", ", ?artistName, " released ", ?title, " in ", STR(YEAR(?year)), "."),
           CONCAT("After ", ?eventName, ", ", ?artistName, " released ", ?title, " in ", STR(YEAR(?year)), ".")
        ) AS ?sentence
    )
}
ORDER BY ?year


#CQ-L1 What type of event was [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
SELECT ?name ?type
WHERE {
    BIND(ex:LiveAid1985 AS ?event)
    ?event a ?type ;
           schema:name ?name .
}


#CQ-L2 How commercially successful was the Band Aid single 'Do They Know It's Christmas?' in the UK?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT ?song ?name ?release ?predicate ?object
WHERE {
  ?song schema:name ?name ;
        schema:datePublished ?release ;
        ?predicate ?object .
  FILTER (?predicate IN (ex:isBestSellingIn, ex:charitySingle))
}

#CQ-L3 Who were the organizers of [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?organizerName
WHERE {
  ex:LiveAid1985 (schema:organizer) ?organizer .
  ?organizer schema:name ?organizerName .
}

#CQ-L4 What were the locations that hosted [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
SELECT DISTINCT ?eventName ?venueName ?city
WHERE {
  ex:LiveAid1985 a schema:MusicEvent;
        schema:name ?eventName ;
        schema:location ?venue .
  ?venue schema:name ?venueName .
  ?venue schema:addressLocality ?city .
  FILTER(?venue != ex:Global)
}
ORDER BY ?city

#CQ-L5 What was the attendance at each venue of [Event]?
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?eventName ?venueName ?attendance
WHERE {
    BIND(ex:LiveAid1985 AS ?event)
    ?event schema:name ?eventName .
    ?subEvent ex:attendance ?attendance ;
              schema:location ?venue .
    OPTIONAL { ?event schema:subEvent ?subEvent }
    OPTIONAL { ?subEvent schema:superEvent ?event }
    FILTER(BOUND(?event))
    ?venue schema:name ?venueName .
}
ORDER BY ?venueName

#CQ-L6 How many satellites were used during [Event]’s broadcast?
PREFIX schema: <http://schema.org/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?eventName ?satelliteNetworkDescription (SUM(?count) AS ?totalSatellites)
WHERE {
    BIND(ex:LiveAid1985 AS ?event)
    ?event schema:name ?eventName .
    ?broadcast a schema:BroadcastEvent ;
               schema:superEvent ?event ;
               ex:facilitatedBy ?satelliteNetwork .
    ?satelliteNetwork schema:hasPart ?fleet .
    ?satelliteNetwork schema:description ?satelliteNetworkDescription .
    ?fleet schema:numberOfItems ?count .
}
GROUP BY ?eventName ?broadcast ?satelliteNetworkDescription

#CQ-L7 What was the global TV audience size for [Event]?
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?eventName ?audienceType ?viewers
WHERE {
    BIND(ex:LiveAid1985 AS ?event)
    ?broadcast a schema:BroadcastEvent ;
               schema:superEvent ?event ;
               schema:audience ?audience .
    ?audience schema:audienceType ?audienceType .
    ?audience ex:viewerCount ?viewers .
    ?event schema:name ?eventName .
}

#CQ-L8 When did [Event] take place?
PREFIX schema: <http://schema.org/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?eventName ?date
WHERE {
    BIND(ex:LiveAid1985 AS ?event)
    ?event schema:name ?eventName .
    ?event schema:startDate ?date .
}

#CQ-L9 Who founded [Event]?
PREFIX schema: <http://schema.org/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?eventName ?founderName
WHERE {
    BIND(ex:LiveAid1985 AS ?event)
    ?event schema:name ?eventName .
    ?event schema:organizer ?founder .
    ?founder schema:name ?founderName .
}

#CQ-L10 What was [Artist]’s full setlist at [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX schema: <http://schema.org/>
SELECT ?artist (GROUP_CONCAT(DISTINCT ?performedSong; separator=", ") AS ?performedSongs)
WHERE {
  ?performance a mm:LivePerformance ;
               mm:isCreatedBy ex:Queen ;
               schema:isPartOf ex:LiveAid1985 ;
               mm:creates ?setlist .
  ?performance mm:isCreatedBy ?performer .
  ?performer schema:name ?artist .
  ?setlist a schema:ItemList ;
           schema:itemListElement ?song .
  ?song schema:name ?performedSong .
}
GROUP BY ?artist

#CQ-L11 Which [Artist] song was the third in their [Event] setlist
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX schema: <http://schema.org/>
SELECT ?songName
WHERE {
  ?performance a mm:LivePerformance ;
               mm:isCreatedBy ex:Queen ;
               schema:isPartOf ex:LiveAid1985 .
  ?subPerf a mm:LivePerformance ;
           schema:isPartOf ?performance ;
           schema:position 2 ;
           mm:creates ?song .
  ?song schema:name ?songName .
}


#CQ-L12 What visual moment involving [Performer] occurred during their performance of [Song]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX schema: <http://schema.org/>
SELECT ?momentDescription
WHERE {
  ?performance a mm:LivePerformance ;
               schema:isPartOf ex:LiveAid1985 .
  ?subPerf a mm:LivePerformance ;
           schema:isPartOf ?performance ;
           mm:isCreatedBy ex:Queen ;
           mm:creates ex:HammerToFall ;
           schema:hasPart ?moment .
  ?moment schema:description ?momentDescription .
}


#CQ-L13 What audience interaction occurred during [Artist]’s performance of [Song] at [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX schema: <http://schema.org/>
SELECT ?interactionDescription
WHERE {
  ?performance a mm:LivePerformance ;
               schema:isPartOf ex:LiveAid1985 .
  ?subPerf a mm:LivePerformance ;
           schema:isPartOf ?performance ;
           mm:isCreatedBy ex:Queen ;
           mm:creates ex:RadioGaGa ;
           schema:hasPart ?interaction .
  ?interaction schema:description ?interactionDescription .
}

#CQ-L14 Who were the founding members of [Artist]?
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?artistName ?founderName ?joinYear
WHERE {
    BIND (ex:Queen as ?artist)
  ?artist a schema:MusicGroup ;
           schema:name ?artistName ;
           schema:foundingDate ?foundingYear .
  ?founder schema:memberOf ?artist ;
           schema:name ?founderName ;
           schema:startDate ?joinYear .
  FILTER(?joinYear = ?foundingYear)
}
ORDER BY ?founderName

#CQ-L15 What [Genre] influenced [Artist]’s early music?
PREFIX schema: <http://schema.org/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?artistName (GROUP_CONCAT(DISTINCT ?genreName; separator=", ") AS ?earlyMusicInfluenceGenre)
WHERE {
    BIND(ex:Queen as ?artist)
  ?artist schema:name ?artistName .
  ?artist ex:earlyMusicInfluence ?genre .
  ?genre schema:name ?genreName .
}
GROUP BY ?artistName



#CQ-L16 How did [Artist]’s musical style evolve over time?
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?artistName
       (GROUP_CONCAT(DISTINCT ?earlyName; separator=", ") AS ?earlyGenres)
       (GROUP_CONCAT(DISTINCT ?laterName; separator=", ") AS ?laterGenres)
WHERE {
    BIND (ex:Queen as ?artist)
  ?artist a schema:MusicGroup ;
           schema:name ?artistName ;
           ex:earlyMusicInfluence ?earlyGenre ;
           mm:hasGenre ?allGenre .
  ?earlyGenre schema:name ?earlyName .
  ?allGenre schema:name ?allName .
  FILTER NOT EXISTS {
    ?artist ex:earlyMusicInfluence ?allGenre .
  }
  BIND(?allName AS ?laterName)
}
GROUP BY ?artistName

#CQ-L17 What [Genre] did [Artist] incorporate later in their career?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT ?artistName
       (GROUP_CONCAT(DISTINCT ?laterName; separator=", ") AS ?laterGenres)
WHERE {
    BIND (ex:Queen as ?artist)
  ?artist a schema:MusicGroup ;
           schema:name ?artistName ;
           mm:hasGenre ?allGenre .
  ?allGenre schema:name ?laterName .
  FILTER NOT EXISTS {
    ?artist ex:earlyMusicInfluence ?allGenre .
  }
}
GROUP BY ?artistName


#CQ-L18 Why is [Artist]’s [Event][Performance] considered iconic?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT ?performanceName
       (GROUP_CONCAT(DISTINCT ?comment; separator=" | ") AS ?fanComments)
       (GROUP_CONCAT(DISTINCT ?body; separator=" | ") AS ?criticReviews)
       (GROUP_CONCAT(DISTINCT ?aspect; separator=", ") AS ?reviewAspects)
WHERE {
  VALUES (?artist ?event) { (ex:Queen ex:LiveAid1985) }
  ?performance a mm:LivePerformance ;
               schema:name ?performanceName ;
               mm:isCreatedBy ?artist ;
               schema:isPartOf ?event ;
               schema:review ?review .
  ?review schema:reviewBody ?body .
  OPTIONAL { ?review schema:reviewAspect ?aspect . }
  OPTIONAL { ?performance schema:comment ?comment . }
}
GROUP BY ?performanceName

#CQ-L19 What was the average length of [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT ?eventName ?duration ?actCount
WHERE {
  ex:LiveAid1985 a schema:MusicEvent ;
                 schema:name ?eventName ;
                 schema:duration ?duration ;
                 ex:numberOfActs ?actCount .
}


#CQ-L20 How many viewers watched [Event] globally?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?eventName ?audienceType ?viewerCount ?countryCount
WHERE {
    BIND(ex:LiveAid1985 as ?event)
    ?event ex:hadBroadcast ?broadcast .
    ?event schema:name ?eventName .
    ?broadcast schema:audience ?audience .
    ?audience schema:audienceType ?audienceType .
    ?audience ex:countryCount ?countryCount .
    ?audience ex:viewerCount ?viewerCount .
}

#CQ-L21 What facilitated the simultaneous global broadcast of [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT DISTINCT ?infrastructureName ?tech
WHERE {
  ?broadcast a schema:BroadcastEvent ;
             schema:broadcastOfEvent ex:LiveAid1985 ;
             ex:usedBroadcastInfrastructure ?infrastructure ;
             ex:distributedVia ?tech .
  # Infrastructure label/name fallback
  OPTIONAL { ?infrastructure rdfs:label ?infraLabel }
  OPTIONAL { ?infrastructure schema:name ?infraNameRaw }
  BIND(COALESCE(?infraLabel, ?infraNameRaw, STRAFTER(STR(?infrastructure), "#")) AS ?infrastructureName)
}
ORDER BY ?infrastructureName ?techName


#CQ-L22,,How were [Performance] scheduled across the [Venue1] and [Venue2] during [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT ?artistName ?venueName ?performanceStartTime
WHERE {
  ?performance a mm:LivePerformance ;
               schema:isPartOf ?event ;
               schema:startDate ?performanceStartTime ;
               mm:isCreatedBy ?artist ;
               schema:location ?venue .
  ?event schema:name ?eventName .
  OPTIONAL { ?artist schema:name ?artistName }
  OPTIONAL { ?venue schema:name ?venueName }
  FILTER(?event = ex:LiveAid1985)
}
ORDER BY ?startTime


#CQ-L23,,What [Song] were performed by [Artist] at [Venue]?
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?artistName ?venueName (GROUP_CONCAT(DISTINCT ?songName; separator=" ") AS ?songsPerfomed)
WHERE {
  ?performance a mm:LivePerformance ;
               mm:isCreatedBy ?artist ;
               schema:isPartOf ?event ;
               schema:location ?venue ;
               mm:creates ?setlist .
  ?artist schema:name ?artistName .
  ?venue schema:name ?venueName .
  ?setlist schema:itemListElement ?song .
  ?song schema:name ?songName .
  FILTER (?artist = ex:Queen)
  FILTER (?event = ex:LiveAid1985)
}
GROUP BY ?artistName ?venueName



#CQ-L24 Which [Organization] preserved the [Venue] broadcast of [Event]?
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT  ?venueName ?broadcastDescription ?broadcastCorpName ?preservationState
WHERE {
  ex:LiveAid1985 ex:hadBroadcast ?broadcast .
  ?broadcast a schema:BroadcastEvent ;
             schema:subEvent ?broadcastVenue .
  ?broadcastVenue a schema:BroadcastEvent ;
                  schema:description ?broadcastDescription ;
                  schema:location ?venue ;
                  schema:provider ?broadcastCorp ;
                  ex:wasBroadcastPreserved ?preservationState .
  ?venue schema:name ?venueName .
  ?broadcastCorp schema:name ?broadcastCorpName .
  FILTER(?preservationState = "true"^^xsd:boolean)
}


#CQ-L25 Which [Organization] deleted the [Venue] broadcast of [Event]?
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT  ?venueName ?broadcastDescription ?broadcastCorpName ?preservationState
WHERE {
  ex:LiveAid1985 ex:hadBroadcast ?broadcast .
  ?broadcast a schema:BroadcastEvent ;
             schema:subEvent ?broadcastVenue .
  ?broadcastVenue a schema:BroadcastEvent ;
                  schema:description ?broadcastDescription ;
                  schema:location ?venue ;
                  schema:provider ?broadcastCorp ;
                  ex:wasBroadcastPreserved ?preservationState .
  ?venue schema:name ?venueName .
  ?broadcastCorp schema:name ?broadcastCorpName .
  FILTER(?preservationState = "false"^^xsd:boolean)
}

#CQ-L26 What was the total amount raised by [Event]?
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?eventName ?amount ?currency
WHERE {
  ex:LiveAid1985 schema:name ?eventName ;
                 ex:raisedAmount ?amountNode .
  ?amountNode schema:value ?amount ;
              schema:currency ?currency .
}


#CQ-L27 Did [Event] serve as a model for subsequent charitable music events?
PREFIX schema: <http://schema.org/>
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
SELECT ?eventName ?inspiredEventName ?inspiredEventType ?inspiredStartDate ?city ?countryName
WHERE {
  ex:LiveAid1985 schema:name ?eventName ;
                 ex:inspiredEvent ?inspiredEvent .
    OPTIONAL { ?inspiredEvent schema:name ?inspiredEventName . }
    OPTIONAL { ?inspiredEvent rdf:type ?inspiredEventType . }
    OPTIONAL { ?inspiredEvent schema:startDate ?inspiredStartDate . }
    OPTIONAL { ?inspiredEvent schema:location ?inspiredLocation . }
    OPTIONAL { ?inspiredLocation schema:name ?city . }
    OPTIONAL { ?inspiredLocation schema:containedInPlace ?country . }
    OPTIONAL { ?country schema:name ?countryName . }
}



















