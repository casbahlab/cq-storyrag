#CQ-ID:CQ-E1 What kind of event was [Event]
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?eventName ?text ?url
WHERE {
    BIND (ex:LiveAid1985 AS ?event)
    ?annotation a oa:Annotation ;
           dct:subject ex:Background ;
           oa:hasTarget ?event ;
           oa:hasBody ?background .
    ?background schema:citation ?url .
    ?background schema:description ?text .
    ?event schema:name ?eventName .
}


#CQ-ID:CQ-E2 Why was [Event] organized?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?eventName ?text ?url
WHERE {
    BIND (ex:LiveAid1985 AS ?event)
    ?annotation a oa:Annotation ;
           dct:subject ex:Background ;
           oa:hasTarget ?event ;
           oa:hasBody ?background .
    ?background schema:citation ?url .
    ?background schema:description ?text .
    ?event schema:name ?eventName .
}


#CQ-ID:CQ-E3 Which [Artist] performed at [Event] held at [Venue]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?sourceUrl ?exactSelector ?refinedBy
WHERE {
    BIND (ex:LiveAid1985 AS ?event)
    BIND (ex:WembleyStadium AS ?venue)
    ?annotation a oa:Annotation ;
           dct:subject ex:Performances ;
           schema:spatial ?venue ;
           oa:hasTarget ?event ;
           oa:hasBody ?background .
    ?background oa:hasSource ?sourceUrl ;
                oa:hasSelector ?secSel .
    OPTIONAL {
        ?secSel oa:refinedBy ?subSel .
        ?secSel oa:exact ?exactSelector .
        OPTIONAL { ?subSel oa:exact ?refinedBy }
        OPTIONAL { ?subSel oa:start ?start ; oa:end ?end }
      }
    ?event schema:name ?eventName .
}


#CQ-ID:CQ-E4 Which [Artist] performed during [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?eventName ?url
WHERE {
    BIND (ex:LiveAid1985 AS ?event)
    ?annotation a oa:Annotation ;
           dct:subject ex:Performances ;
           oa:hasTarget ?event ;
           oa:hasBody ?background .
    ?background schema:citation ?url .
    ?event schema:name ?eventName .
}

#CQ-ID:CQ-E5 Which [City] or [Country] hosted [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
SELECT DISTINCT ?eventName ?locationName ?cityName ?countryName
WHERE {
    ?event a schema:MusicEvent ;
           schema:location ?location .
    ?event schema:name ?eventName .
    OPTIONAL { ?location schema:location ?city .
        	   ?location schema:name ?locationName .
    			}
    OPTIONAL { ?city schema:containedInPlace ?country .
        		?city schema:name ?cityName .
        		?country schema:name ?countryName .
    			}
    FILTER(?event =   ex:LiveAid1985)
}

#CQ-ID:CQ-E6 Which [Venue] hosted [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
SELECT DISTINCT ?eventName (GROUP_CONCAT(DISTINCT ?venueName; separator=", ") AS ?allVenues)
WHERE {
    ?event a schema:MusicEvent ;
           schema:location ?venue .
    ?venue a schema:Place ;
           schema:name ?venueName .
    ?event schema:name ?eventName .
    FILTER(?event =   ex:LiveAid1985)
}
GROUP BY ?eventName


#CQ-ID:CQ-E7 What role did television and media coverage play in [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?eventName ?text ?url
WHERE {
    BIND (ex:LiveAid1985 AS ?event)
    ?annotation a oa:Annotation ;
           dct:subject ex:Broadcasts ;
           oa:hasTarget ?event ;
           oa:hasBody ?broadcast .
    ?broadcast schema:citation ?url .
    ?broadcast schema:description ?text .
    ?event schema:name ?eventName .
}


#CQ-ID:CQ-E9 Which [Performance] at [Event] is considered iconic?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?eventName ?iconicPerformanceDescription
WHERE {
    ?performance a mm:LivePerformance ;
                 schema:isPartOf ?event ;
    			 ex:isIconic ?isIconic .
    ?performance schema:description ?iconicPerformanceDescription .
    ?event schema:name ?eventName .
    FILTER(?event =   ex:LiveAid1985)
    FILTER(?isIconic = true)
}


#CQ-ID:CQ-E10 Which [Song] did [Artist] perform at [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT (GROUP_CONCAT(DISTINCT ?workName; separator=", ") AS ?allWorkName)
WHERE {
    ?performance a mm:LivePerformance ;
                 schema:isPartOf ?event ;
    			 ex:isIconic ?isIconic .
    ?performance mm:performedWork ?work .
    ?performance schema:performer ?artist .
    ?work schema:name ?workName .
    FILTER(?event =   ex:LiveAid1985)
    FILTER(?artist = ex:Queen)
}



#CQ-ID:CQ-E11 When did [Artist] debut?,[Artist] debuted in [Year] with a performance or release in [City/Country].
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?artistName ?debutDate ?debutAlbumName (GROUP_CONCAT(DISTINCT ?featuredSongsName; separator=", ") AS ?albumFeaturedSongs)
WHERE {
    BIND(ex:Madonna as ?artist)
    ?album a schema:MusicAlbum ;
            schema:byArtist ?artist ;
            ex:isDebutAlbum true .
    ?artist schema:name ?artistName .
    OPTIONAL {?album schema:datePublished ?debutDate .}
    OPTIONAL {?album schema:name ?debutAlbumName .}
    OPTIONAL {?album schema:hasPart ?featuredSongs .}
    OPTIONAL {?featuredSongs schema:name ?featuredSongsName .}
}
GROUP BY ?artistName ?album ?debutDate ?debutAlbumName


#CQ-ID:CQ-E12 When and where was [Artist] formed?,[Artist] was formed in [Year] in [City/Country].
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT ?artistName ?foundingDate ?foundingLocation
WHERE {
    BIND (ex:Queen as ?artist)
    ?artist a mm:MusicArtist .
    ?artist schema:foundingDate ?foundingDate .
    ?artist schema:foundingLocation ?foundingLocation .
    ?artist schema:name ?artistName .
}

#CQ-ID:CQ-E12b When and where was [Artist] born?,[Artist] was born in [Year] in [City/Country].
PREFIX ex:     <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
SELECT ?artistName ?birthDate ?birthPlace
WHERE {
  BIND(ex:Madonna AS ?artist)

  ?artist schema:name ?artistName .
  ?artist schema:birthDate ?birthDate .
  OPTIONAL { ?artist schema:birthPlace ?birthPlace }
}


#CQ-ID:CQ-E13 Who were the members of [Artist]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?artistName (GROUP_CONCAT(DISTINCT ?memberName; separator=", ") AS ?allMembers)
WHERE {
    ?artist schema:name ?artistName .
    OPTIONAL {
        ?artist schema:member ?member .
        ?member schema:name ?memberName .
    }
    FILTER(?artist =   ex:Queen)
}
GROUP BY ?artistName


#CQ-ID:CQ-E14 Who were the solo artists that performed at [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?eventName (GROUP_CONCAT(DISTINCT ?artistName; separator=", ") AS ?allPerformingArtists)
WHERE {
    ?performance a schema:Event, mm:LivePerformance ;
           schema:isPartOf ?event ;
           schema:performer ?artist .
    ?event schema:name ?eventName .
    ?artist a schema:Person .
    ?artist schema:name ?artistName .
    FILTER(?event =   ex:LiveAid1985)
}
GROUP BY ?eventName



#CQ-ID:CQ-E15 What [Genre] are associated with [Artist]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?artistName (GROUP_CONCAT(DISTINCT ?genreLabel; separator=", ") AS ?allGenres)
WHERE {
    ?artist schema:name ?artistName .
    { ?artist schema:genre ?genre } UNION { ?artist mm:hasGenre ?genre } .
    OPTIONAL { ?genre schema:name ?genreLabel . }
    OPTIONAL { ?genre rdfs:label ?genreLabel . }
    BIND(STRAFTER(STR(?genre), "#") AS ?localName)
    FILTER(?artist =   ex:Queen)
}
GROUP BY ?artistName

#CQ-ID:CQ-E16 What enabled the global broadcast of [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?eventName ?text ?url
WHERE {
    BIND (ex:LiveAid1985 AS ?event)
    ?annotation a oa:Annotation ;
           dct:subject ex:Broadcasts ;
           oa:hasTarget ?event ;
           oa:hasBody ?broadcast .
    ?broadcast schema:citation ?url .
    ?broadcast schema:description ?text .
    ?event schema:name ?eventName .
}


#CQ-ID:CQ-E17 What were the challenges in organizing [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?eventName ?challengeText ?challengeUrl
WHERE {
    BIND (ex:LiveAid1985 AS ?event)
    ?annotation a oa:Annotation ;
           dct:subject ex:Challenge ;
           oa:hasTarget ?event ;
           oa:hasBody ?challenge .
    ?challenge schema:citation ?challengeUrl .
    ?challenge schema:description ?challengeText .
    ?event schema:name ?eventName .
}


#CQ-ID:CQ-E18 How did [Event] influence future benefit concerts?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?eventName ?text ?url
WHERE {
    BIND (ex:LiveAid1985 AS ?event)
    ?annotation a oa:Annotation ;
           dct:subject ex:Legacy ;
           oa:hasTarget ?event ;
           oa:hasBody ?legacy .
    ?legacy schema:citation ?url .
    ?legacy schema:description ?text .
    ?event schema:name ?eventName .
}


#CQ-ID:CQ-E19 What were the reactions of the audience or media to [Artist]’s [Performance] at [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX schema: <http://schema.org/>
SELECT DISTINCT ?artistName ?eventName ?reviewerName ?reviewText ?url
WHERE {
    ?performance schema:performer ?artist ;
                 schema:isPartOf|schema:isPartOf ?event .
    ?artist schema:name ?artistName .
    ?event schema:name ?eventName .
    OPTIONAL {
        ?review schema:itemReviewed ?performance .
        ?review schema:reviewBody ?reviewText .
        ?review schema:author ?reviewer .
        ?reviewer schema:name ?reviewerName .
        ?review schema:url ?url
    }
    OPTIONAL {
        ?performance schema:comment ?audienceComment
    }
    FILTER(?artist =   ex:Queen)
    FILTER(?event =   ex:LiveAid1985)
}


#CQ-ID:CQ-E20  How much money was raised by [Event]?,
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?eventName ?text ?url
WHERE {
    BIND (ex:LiveAid1985 AS ?event)
    ?annotation a oa:Annotation ;
           dct:subject ex:Fundraising ;
           oa:hasTarget ?event ;
           oa:hasBody ?fundraising .
    ?fundraising schema:citation ?url .
    ?fundraising schema:description ?text .
    ?event schema:name ?eventName .
}

#CQ-ID:CQ-E21 Which [Organization] archived the broadcast of [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?eventName (GROUP_CONCAT(DISTINCT ?broadcastingCompanyName; separator=", ") AS ?broadcastingCompanies)
WHERE {
    BIND (  ex:LiveAid1985 as ?event )
    ?event ex:hadBroadcast ?broadcast .
    ?event schema:name ?eventName .
    OPTIONAL { ?broadcast schema:provider ?broadcastingCompany . }
    OPTIONAL { ?broadcastingCompany schema:name ?broadcastingCompanyName }
    OPTIONAL { ?broadcastingCompany rdfs:label ?broadcastingCompanyName }
}
GROUP BY ?eventName


#CQ-ID:CQ-E22 Which[Organization] failed to archive the broadcast of [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT ?eventName ?subBroadcastDescription ?broadcastCompanyName ?preserved
WHERE {
    BIND (  ex:LiveAid1985 as ?mainEvent)
    ?mainEvent schema:name ?eventName .
    ?mainEvent ex:hadBroadcast ?mainBroadcast .
    ?mainBroadcast schema:subEvent ?subBroadcast .
    ?subBroadcast schema:description ?subBroadcastDescription .
    ?subBroadcast schema:provider ?broadcastCompany .
    ?subBroadcast ex:wasBroadcastPreserved ?preserved .
    OPTIONAL { ?broadcastCompany schema:name ?broadcastCompanyName }
    OPTIONAL { ?broadcastCompany rdfs:label ?broadcastCompanyName }
    FILTER(?preserved = false)
}
ORDER BY DESC(?preserved)


#CQ-ID:CQ-E23 What is the preservation status of [Event] broadcasts at [Venue]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?eventName ?subBroadcastDescription ?broadcastCompanyName ?preserved
WHERE {
    VALUES ?mainEvent {   ex:LiveAid1985 }
    ?mainEvent ex:hadBroadcast ?mainBroadcast .
    ?mainEvent schema:name ?eventName .
    ?mainBroadcast schema:subEvent ?subBroadcast .
    ?subBroadcast schema:description ?subBroadcastDescription .
    ?subBroadcast schema:provider ?broadcastCompany .
    ?subBroadcast ex:wasBroadcastPreserved ?preserved .
    OPTIONAL { ?broadcastCompany schema:name ?broadcastCompanyName }
    OPTIONAL { ?broadcastCompany rdfs:label ?broadcastCompanyName }
}
ORDER BY DESC(?preserved)

#CQ-ID:CQ-E24 Why is [Artist]’s performance at [Event] considered iconic?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT ?artistName ?reason ?url
WHERE {
    BIND (ex:Queen as ?artist)
    BIND (ex:LiveAid1985 as ?event)
    ?performance a mm:LivePerformance ;
                 schema:isPartOf   ?event ;
                 schema:performer   ?artist .
    ?artist schema:name ?artistName .
    ?review schema:itemReviewed ?performance .
    ?review schema:reviewBody ?reason .
    ?review schema:url ?url .
}

#CQ-ID:CQ-E27 What creative milestones defined [Artist]’s career after [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?eventName ?artistName ?text ?url
WHERE {
    BIND (ex:LiveAid1985 AS ?event)
    BIND (ex:Queen AS ?artist)
    ?annotation a oa:Annotation ;
           dct:subject ex:CareerMilestones ;
           oa:hasTarget ?artist ;
           oa:hasBody ?fundraising .
    ?fundraising schema:citation ?url .
    ?fundraising schema:description ?text .
    ?event schema:name ?eventName .
    ?artist schema:name ?artistName .
}

#CQ-ID:CQ-L4 What were the locations that hosted [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
SELECT DISTINCT ?eventName ?venueName ?city
WHERE {
    ex:LiveAid1985 a schema:MusicEvent;
        schema:name ?eventName ;
        schema:location ?venue .
  ?venue schema:name ?venueName .
  ?venue schema:addressLocality ?city .
  FILTER(?venue != ex:Global)
}
ORDER BY ?city

#CQ-ID:CQ-L5 What was the attendance at each venue of [Event]?
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?eventName ?venueName ?attendance
WHERE {
    BIND(  ex:LiveAid1985 AS ?event)
    ?event schema:name ?eventName .
    ?subEvent ex:attendance ?attendance ;
              schema:location ?venue .
    OPTIONAL { ?event schema:subEvent ?subEvent }
    OPTIONAL { ?subEvent schema:superEvent ?event }
    FILTER(BOUND(?event))
    ?venue schema:name ?venueName .
}
ORDER BY ?venueName

#CQ-ID:CQ-L6 How many satellites were used during [Event]’s broadcast?
PREFIX schema: <http://schema.org/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?eventName ?satelliteNetworkDescription (SUM(?count) AS ?totalSatellites)
WHERE {
    BIND(  ex:LiveAid1985 AS ?event)
    ?event schema:name ?eventName .
    ?broadcast a schema:BroadcastEvent ;
               schema:superEvent ?event ;
               ex:facilitatedBy ?satelliteNetwork .
    ?satelliteNetwork schema:hasPart ?fleet .
    ?satelliteNetwork schema:description ?satelliteNetworkDescription .
    ?fleet schema:numberOfItems ?count .
}
GROUP BY ?eventName ?broadcast ?satelliteNetworkDescription

#CQ-ID:CQ-L7 What was the global TV audience size for [Event]?
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?eventName ?audienceType ?viewers
WHERE {
    BIND(  ex:LiveAid1985 AS ?event)
    ?broadcast a schema:BroadcastEvent ;
               schema:superEvent ?event ;
               schema:audience ?audience .
    ?audience schema:audienceType ?audienceType .
    ?audience ex:viewerCount ?viewers .
    ?event schema:name ?eventName .
}

#CQ-ID:CQ-L8 When did [Event] take place?
PREFIX schema: <http://schema.org/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?eventName ?date
WHERE {
    BIND(  ex:LiveAid1985 AS ?event)
    ?event schema:name ?eventName .
    ?event schema:startDate ?date .
}

#CQ-ID:CQ-L9 Who founded [Event]?
PREFIX schema: <http://schema.org/>
PREFIX ex: <http://wembrewind.live/ex#>
SELECT ?eventName ?founderName
WHERE {
    BIND(  ex:LiveAid1985 AS ?event)
    ?event schema:name ?eventName .
    ?event schema:organizer ?founder .
    ?founder schema:name ?founderName .
}

#CQ-ID:CQ-L10 What was [Artist]’s full setlist at [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX schema: <http://schema.org/>
SELECT ?eventName ?artistName ?song ?position
WHERE {
    BIND (ex:LiveAid1985 AS ?event)
    BIND (ex:Queen AS ?artist)
  ?performance a mm:LivePerformance ;
               schema:performer   ?artist ;
               schema:isPartOf   ?event ;
               ex:setList ?setlist .
  ?setlist schema:itemListElement ?listItem .
  ?listItem schema:item ?work .
  ?listItem schema:position ?position .
  ?work schema:name ?song .
  ?artist schema:name ?artistName .
  ?event schema:name ?eventName .
}
ORDER BY ?position

#CQ-ID:CQ-L11 Which [Artist] song was the third in their [Event] setlist
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX schema: <http://schema.org/>
SELECT ?eventName ?artistName ?song ?position
WHERE {
    BIND (ex:LiveAid1985 AS ?event)
    BIND (ex:Queen AS ?artist)
    BIND (3 AS ?position)
  ?performance a mm:LivePerformance ;
               schema:performer   ?artist ;
               schema:isPartOf   ?event ;
               ex:setList ?setlist .
  ?setlist schema:itemListElement ?listItem .
  ?listItem schema:item ?work .
  ?listItem schema:position ?position .
  ?work schema:name ?song .
  ?artist schema:name ?artistName .
  ?event schema:name ?eventName .
}
ORDER BY ?position

#CQ-ID:CQ-L14 Who were the founding members of [Artist]?
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?artistName (GROUP_CONCAT(DISTINCT ?founderName; separator=", ") AS ?foundingMembers)
WHERE {
    BIND (ex:Queen AS ?artist)
    ?artist a mm:MusicArtist ;
            schema:name ?artistName ;
            schema:foundingDate ?foundingYear .
    ?role a schema:Role, mm:MusicEnsembleMembership ;
          mm:involvesMusicEnsemble ?artist ;
          mm:involvesMemberOfMusicEnsemble ?member ;
          ex:isOriginalMember true .
    ?member schema:name ?founderName .
}
GROUP BY ?artistName


#CQ-ID:CQ-L15 What [Genre] influenced [Artist]’s music?
PREFIX schema: <http://schema.org/>
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT ?artistName (GROUP_CONCAT(DISTINCT ?genreName; separator=", ") AS ?earlyMusicInfluenceGenre)
WHERE {
    BIND(  ex:Queen as ?artist)
  ?artist schema:name ?artistName .
  ?artist mm:hasGenre ?genre .
  ?genre schema:name ?genreName .
}
GROUP BY ?artistName


#CQ-ID:CQ-L19 What was the average length of [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT ?eventName ?duration ?actCount
WHERE {
    ex:LiveAid1985 a schema:MusicEvent ;
                 schema:name ?eventName ;
                 schema:duration ?duration ;
                 ex:numberOfActs ?actCount .
}


#CQ-ID:CQ-L20 How many viewers watched [Event] globally?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
SELECT ?eventName ?audienceType ?viewerCount ?countryCount
WHERE {
    BIND(  ex:LiveAid1985 as ?event)
    ?event ex:hadBroadcast ?broadcast .
    ?event schema:name ?eventName .
    ?broadcast schema:audience ?audience .
    ?audience schema:audienceType ?audienceType .
    ?audience ex:countryCount ?countryCount .
    ?audience ex:viewerCount ?viewerCount .
}

#CQ-ID:CQ-L21 What facilitated the simultaneous global broadcast of [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?eventName ?text ?url
WHERE {
    BIND (ex:LiveAid1985 AS ?event)
    ?annotation a oa:Annotation ;
           dct:subject ex:Satellite ;
           oa:hasTarget ?event ;
           oa:hasBody ?satellite .
    ?satellite schema:citation ?url .
    ?satellite schema:description ?text .
    ?event schema:name ?eventName .
}


#CQ-ID:CQ-L22  How were [Performance] scheduled across the [Venue1] and [Venue2] during [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX oa: <http://www.w3.org/ns/oa#>
PREFIX dct: <http://purl.org/dc/terms/>
SELECT ?eventName ?text ?url
WHERE {
    BIND (ex:LiveAid1985 AS ?event)
    ?annotation a oa:Annotation ;
           dct:subject ex:Performances ;
           oa:hasTarget ?event ;
           oa:hasBody ?performances .
    ?performances schema:citation ?url .
    ?performances schema:description ?text .
    ?event schema:name ?eventName .
}


#CQ-ID:CQ-E28  What instruments were used in [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?instrumentName
WHERE {
  ?membership a schema:Role,
        mm:MusicEnsembleMembership ;
        schema:instrument ?instrument .
  OPTIONAL { ?instrument schema:name ?instrumentName }
}
ORDER BY ?instrumentName


#CQ-ID:CQ-L28  Which instruments did [MusicArtist] play during [Event]?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?instrumentName
WHERE {
    BIND (ex:BrianMay as ?guitarist)
  ?membership a schema:Role,
        mm:MusicEnsembleMembership ;
        mm:involvesMemberOfMusicEnsemble ?guitarist ;
        schema:instrument ?instrument .
  OPTIONAL { ?instrument schema:name ?instrumentName }
}
ORDER BY ?instrumentName


#CQ-ID:CQ-H-AF1 What are some notable facts about the music artist {artist}?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
SELECT DISTINCT ?artist ?songfactsUrl
WHERE {
  BIND (ex:Madonna AS ?artist)
  ?artist a ?type ;
          schema:sameAs ?songfactsUrl .
  VALUES ?type { schema:MusicGroup schema:Person }
  FILTER( STRSTARTS(LCASE(STR(?songfactsUrl)), "https://www.songfacts.com/facts") )
}


#CQ-ID:CQ-H-SF1 What are some notable facts about the songs by {artist}?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?artist ?songfactsUrl
WHERE {
  BIND (ex:Queen AS ?artist)
  ?performance a mm:LivePerformance ;
          schema:performer ?artist ;
          mm:performedWork ?works .
  ?works schema:sameAs ?songfactsUrl .
  FILTER( STRSTARTS(LCASE(STR(?songfactsUrl)), "https://www.songfacts.com/facts") )
}


#CQ-ID:CQ-H-GS1 What are the lyrics to the songs performed by {artist}?
PREFIX ex: <http://wembrewind.live/ex#>
PREFIX schema: <http://schema.org/>
PREFIX mm: <https://w3id.org/polifonia/ontology/music-meta/>
SELECT DISTINCT ?artist ?songfactsUrl
WHERE {
  BIND (ex:Queen AS ?artist)
  ?performance a mm:LivePerformance ;
          schema:performer ?artist ;
          mm:performedWork ?works .
  ?works schema:sameAs ?songfactsUrl .
  FILTER( STRSTARTS(LCASE(STR(?songfactsUrl)), "https://genius.com") )
}